#!/usr/bin/wish

. configure -padx 5 -pady 5

namespace eval rift {
	variable time 0
	variable timerLabel "XX:XX.XX"
	variable currentStartTime 0
	variable currentTimerLabel "XX:XX.XX"

	proc updateTimer {} {
		variable time
		variable timerLabel
		variable currentStartTime
		variable currentTimerLabel

		set timerLabel [formatDuration $time]
		set currentTimerLabel [formatDuration [expr $time - $currentStartTime]]

		set item [.splits selection]
		while {$item ne {}} {
			.splits set $item time $timerLabel
			set item [.splits parent $item]
		}
	}

	proc formatDuration {micros} {
		set CENTI  10000
		set SECOND [expr $CENTI * 100]
		set MINUTE [expr $SECOND * 60]
		set HOUR   [expr $MINUTE * 60]

		set hrs    [expr $micros / $HOUR]
		set micros [expr $micros % $HOUR]
		set mins   [expr $micros / $MINUTE]
		set micros [expr $micros % $MINUTE]
		set secs   [expr $micros / $SECOND]
		set micros [expr $micros % $SECOND]
		set centis [expr $micros / $CENTI]

		set fmt [format "%.2d" $centis]
		set fmt [formatPart {$hrs || $mins} 1 $secs . $fmt]
		set fmt [formatPart $hrs $mins $mins : $fmt]
		set fmt [formatPart 0 $hrs $hrs : $fmt]
		return $fmt
	}
	proc formatPart {cond1 cond2 part sep fmt} {
		if {[uplevel 1 expr $cond1]} {
			format "%.2d%s%s" $part $sep $fmt
		} elseif {[uplevel 1 expr $cond2]} {
			format "%d%s%s" $part $sep $fmt
		} else {
			return $fmt
		}
	}

	proc title {game category} {
		ttk::label .title -style Title.TLabel -text "$game\n$category"
		pack .title -fill x
	}

	proc timer {} {
		ttk::frame .timer -borderwidth 0
		pack .timer -fill x
		grid columnconfigure .timer 0 -weight 1
		grid columnconfigure .timer 1 -weight 0

		ttk::label .timer.main -style Timer.TLabel -textvariable ::rift::timerLabel
		grid .timer.main -column 0 -columnspan 2 -row 0 -sticky e

		ttk::label .timer.currentLabel -style Current.Timer.TLabel -text "Current Split:"
		grid .timer.currentLabel -column 0 -row 1 -sticky e -padx 3
		ttk::label .timer.current -style Current.Timer.TLabel -textvariable ::rift::currentTimerLabel
		grid .timer.current -column 1 -row 1 -sticky e
	}

	proc splits {config} {
		ttk::treeview .splits \
			-style Splits.Treeview \
			-selectmode none -show tree \
			-columns {delta time}

		.splits column delta -width 50 -stretch false
		.splits column time -width 85 -stretch false

		bind .splits <<TreeviewSelect>> ::rift::updateSplit
		pack .splits -fill both -expand true

		parseSplits {} $config
	}
	proc parseSplits {parent config} {
		foreach {name body} $config {
			set item [.splits insert $parent end -text $name]
			if {$body ne "."} {
				parseSplits $item $body
			}
		}
	}
	proc updateSplit {} {
		# Close everything
		set item [.splits selection]
		while true {
			set item [.splits parent $item]
			foreach child [.splits children $item] {
				.splits item $child -open false
			}
			if {$item eq {}} break
		}

		# Open just enough to see the new active split
		.splits see [.splits selection]
	}
	proc timerStarted {} {
		return [llength [.splits selection]]
	}

	proc splitter {path} {
		set f [open $path {RDONLY NONBLOCK}]
		chan configure $f -blocking false
		chan event $f readable "::rift::readEvent $f"
	}
	proc readEvent {f} {
		variable time
		variable currentStartTime

		if {[chan gets $f ev] < 0} {
			chan event $f readable
			error "Splitter disconnected"
			exit 1
		}
		set time [lindex $ev 0]
		set type [lindex $ev 1]

		switch -nocase -- $type {
			BEGIN {
				resetTimes .splits {}
				set currentStartTime $time
				.splits selection set [firstLeaf .splits {}]
			}

			RESET {
				resetTimes .splits {}
				.splits selection set {}
				set currentStartTime 0
				# Always update the timer after a RESET
				updateTimer
			}

			SPLIT {
				set item [.splits selection]
				set currentStartTime $time
				.splits selection set [nextLeaf .splits $item]
			}
		}

		if {[timerStarted]} {
			updateTimer
		}
	}

	proc firstLeaf {pathname item} {
		set children [$pathname children $item]
		while {[llength $children]} {
			set item [lindex $children 0]
			set children [$pathname children $item]
		}
		return $item
	}
	proc nextLeaf {pathname item} {
		while {[$pathname next $item] eq {}} {
			set item [$pathname parent $item]
			if {$item eq {}} {
				return
			}
		}
		set item [$pathname next $item]
		return [firstLeaf $pathname $item]
	}

	proc resetTimes {pathname item} {
		foreach item [$pathname children $item] {
			$pathname set $item time {}
			resetTimes $pathname $item
		}
	}

	# Default styles
	array set styles {
		background #fff

		title {-font "Helvetica 14"}
		timer {-font "Helvetica 24 bold"}
		timer.ahead {-foreground #64ff64}
		timer.behind {-foreground #ff3232}
		timer.current {-font "Helvetica 16"}

		splits {-font "Helvetica 12"}
		splits.best {-foreground #ffdc00}
		splits.aheadGain {-foreground #50d200}
		splits.aheadLose {-foreground #b4ff78}
		splits.behindGain {-foreground #c80000}
		splits.behindLose {-foreground #ffdc00}
	}
	proc style {name args} {
		# Attempt an access to ensure the key is valid
		nop $::rift::styles($name)

		set ::rift::styles($name) $args
	}
	proc applyStyles {} {
		set background [lindex $::rift::styles(background) 0]
		. configure -background $background
		ttk::style configure . -background $background

		ttk::style configure Title.TLabel {*}$::rift::styles(title)

		ttk::style configure Timer.TLabel {*}$::rift::styles(timer)
		ttk::style configure Ahead.Timer.TLabel {*}$::rift::styles(timer.ahead)
		ttk::style configure Behind.Timer.TLabel {*}$::rift::styles(timer.ahead)
		ttk::style configure Current.Timer.TLabel {*}$::rift::styles(timer.current)

		ttk::style configure Splits.Treeview \
			-background $background -fieldbackground $background \
			{*}$::rift::styles(splits)
		ttk::style layout Splits.Treeview {
			Treeview.padding -sticky nsew -border 0 -children {
				Treeview.treearea -sticky nsew -border 0 -expand true
			}
		}
		.splits tag configure best {*}$::rift::styles(splits.best)
		.splits tag configure aheadGain {*}$::rift::styles(splits.aheadGain)
		.splits tag configure aheadLose {*}$::rift::styles(splits.aheadLose)
		.splits tag configure behindGain {*}$::rift::styles(splits.behindGain)
		.splits tag configure behindLose {*}$::rift::styles(splits.behindLose)
	}
	proc aliasStyles {} {
		foreach name [array names ::rift::styles] {
			uplevel 1 "proc $name args {::rift::style $name {*}\$args}"
		}
	}

	proc nop args {}
	proc alias {name} {
		uplevel 1 "proc $name args {::rift::$name {*}\$args}"
	}
}

namespace eval riftGameConfig {
	::rift::alias title
	::rift::alias timer
	::rift::alias splits
	::rift::alias splitter
}

namespace eval riftStyleConfig {
	::rift::aliasStyles
}

# Parse args
if {[llength $argv] != 1} {
	puts "Usage: $argv0 GAME"
	puts ""
	puts "Available games:"
	foreach game [glob -directory games -tails -types d *] {
		puts " - $game"
	}
	exit 1
}
namespace eval riftGameConfig {
	source "games/[lindex $::argv 0]/config.tcl"
}
namespace eval riftStyleConfig {
	source "style.tcl"
}
::rift::applyStyles
